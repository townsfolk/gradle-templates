buildscript {
    dependencies {
        classpath "com.blackbaud:gradle-internal:3.+"
    }
}

apply plugin: "blackbaud-internal"
apply plugin: "blackbaud-templates"
apply plugin: "integration-test"

setGradleVersions()
println "Using gradle version: ${gradleVersion}"

dependencies {
    compileOnly gradleApi()
    compile localGroovy()
    compile 'com.google.guava:guava:18.0'
    compile 'org.eclipse.jgit:org.eclipse.jgit:4.0.2.201509141540-r'
    compile "org.gradle:gradle-tooling-api:${gradleVersion}"

    testCompile gradleTestKit()
    testCompile ('com.blackbaud:gradle-internal-test:4.+') {
        exclude group: 'org.spockframework'
    }
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile 'junit:junit:4.11'
}

check.dependsOn integrationTest

def setGradleVersions() {
    def group
    try {
      def props = new Properties()
      File propFile = new File('gradle/wrapper/gradle-wrapper.properties')
      props.load(propFile.newDataInputStream())
      String distUrl = props.getProperty('distributionUrl')
      group = (distUrl =~ "gradle-blackbaud-((.*)-bb.[0-9]+.[0-9]+)-bin.zip")
    } catch (Exception ex) {
      throw new RuntimeException("Failed to parse gradle version from gradle-wrapper.properties. Please ensure distributionUrl is set!")
    }

    ext {
        gradleVersion=group[0][2]
        blackbaudGradleVersion=group[0][1]
    }
}
